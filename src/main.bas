100 REM KALLE -KOTIPSYKIATRI
120 REM (C) P. TOLONEN 82-04
170 GOSUB380:REM ALKU
190 GOSUB320:REM LUE LAUSE
200 IF L$=" LOPPU "THENGOTO270
220 GOSUB830:REM TEE VASTAUS
230 GOSUB1600:REM ARKISTOI
240 GOSUB760:REM JA VASTAA
250 GOTO190:REM   -->LOOP
270 GOSUB640:REM LOPETUS
280 END
290 ::
300 REM LUE LAUSE
310 ::
320 PRINTTAB(SY+1):INPUTL$:IFL$=""THENGOTO320
330 L$=" "+L$+" ":RETURN
350 ::
360 REM ALKU
370 ::
380 KS=17:KP=2:AM=41:SM=7
381 LM=11
385 PRINT"{clr}{crsr down}{crsr down}{crsr down}{crsr down}{crsr down}{crsr down}{crsr down}{crsr down}{crsr down}{crsr right}{crsr right}{crsr right}{crsr right}{crsr right}{crsr right}{crsr right}{crsr right}{crsr right}{crsr right}{crsr right}HETKI..."
390 DEF FNS(S)=INT(RND(-TI)*S+1)
400 DIM KS$(KS,1),KP$(KP,1),MV$(5),MV%(5),LI$(30)
405 DIM SN$(AM,SM),LM$(AM,LM),AE%(AM)
410 REM === KAANTOSANAT === KS
420 FORI=1TOKS:READKS$(I,0),KS$(I,1):NEXT
430 DATA MINA,SINA,SINA,MINA,ME,TE,MINUN,SINUN,SINUN,MINUN,MEIDAN,TEIDAN,SINUT
431 DATA MINUT,MEIDAT,TEIDAT,SINUA,MINUA,MINUA,MINUA
440 DATA OLEN,OLET,OLET,OLEN,MINULLE,SINULLE,SINULLE,MINULLE,MINUT,SINUT
441 DATA TEIDAT,MEIDAT,OLEMME,OLETTE
450 REM === KAANTOPAATTEET === KP
460 FORI=1TOKP:READKP$(I,0),KP$(I,1):NEXT
470 DATA NI,SI,SI,NI
480 REM === AVMT JA VSTKST ===
490 FORI=0TOAM:READAS$:IFAS$="L"THENAM=I-1:GOTO570
500 GOSUB1360
510 FORJ=1TOLM+1:READL$
515 IFL$="@"THENLM$(I,0)=STR$(J-1)+"*"+STR$(FNS(J)-1):GOTO540
520 LM$(I,J)=L$
530 NEXTJ
540 NEXTI:RESTORE
550 REM*** INTRO ***
570 PRINT"{clr}{crsr down}TERVE,OLEN KALLE JA KUTISTAN KALLOSI JA LUOTAAN TAJUNTASI TASOJA."
575 PRINT"{crsr down}NO NIIN - RENTOUDUHAN NIIN ALOITAMME"
576 PRINT"ISTUNNON. JOS KUITENKIN ENSIKSI ANTAISIT NIMESI TANNE REKISTERIIN."
577 PRINT
578 PRINT
581 INPUTN$:IFN$=""OR N$=" " THEN 581
590 PRINT"{crsr down}HIENOA ";N$;" , KERROHAN NYT MINULLE MIKA ON ONGELMASI"
600 PRINT:RETURN
610 ::
620 REM    LOPETUS
630 ::
640 K=0
650 PRINT"{clr}{crsr down}   K E S K U S T E L U N A I H E E T"
660 FORI=0TO19:IFI+K>AMTHENGOTO720
670 PRINTI+K;"..";LEFT$(SN$(I+K,1),10);
680 PRINTTAB(16)"I";
690 FORJ=0TOAE%(I+K):PRINT"{rvs on}+{rvs off}";:NEXT
700 PRINTAE%(I+K)
710 NEXT:PRINT"{crsr down}JATKA...";
711 GETA$:IFA$=""THENGOTO711
712 K=K+1:GOTO650
720 RETURN
730 ::
740 REM     VASTAA
750 ::
760 IFNOT(LOPUSSA$="KOODI")THENLI$=""
770 PRINTTAB(SY+1);"-";VA$;LI$
780 VA$="":LI$="":RETURN
800 ::
810 REM     TEE VASTAUS
820 ::
830 GOSUB1900:REM TOISTOA?
840 IFTS$="ON"THENGOTO870
850 GOSUB1230:REM ONKO AVAIN?
860 IF A$="AVAIN EI"THENGOSUB1700:GOSUB1490:GOTO890
870 GOSUB1490:REM LUE VASTAUS
880 GOSUB930:REM TEE LIITOS
890 RETURN
900 REM
910 REM***  TEE LIITOS  ***
920 REM
930 GOSUB1060:REM KAS.LIITOS
940 IFLI$=" "THENGOTO970
950 LL$=LEFT$(LI$,LEN(LI$)-1):GOSUB1110:REM KAANNA
970 RETURN
980 ::
990 FORI=1TOB
1000 FORJ=1TOKS:IFLI$(I)=KS$(J,0)THENLI$(I)=KS$(J,1):GOTO1020
1010 NEXT
1020 FORK=1TOKP
1021 S$=KP$(K,0):IFRIGHT$(LI$(I),LEN(S$))=S$THENXX=1
1022 IFXX=1THENLI$(I)=LEFT$(LI$(I),LEN(LI$(I))-LEN(S$))+KP$(K,1):XX=0:GOTO1040
1030 NEXT
1040 NEXT:RETURN
1045 REM
1050 LI$="":FORI=1TOB:LI$=LI$+" "+LI$(I):NEXT:RETURN
1055 REM
1060 LL$="":LI$=RIGHT$(L$,LEN(L$)-L)+" "
1070 FORI=1TOLEN(LI$)
1075 IFMID$(LI$,I,1)=" "THENLI$=RIGHT$(LI$,LEN(LI$)-I):GOTO1090
1080 NEXT
1090 RETURN
1091 ::
1100 REM KAANNA SANAT, PAATTEET
1105 ::
1110 GOSUB1160:REM LI$->LI$()
1120 GOSUB990 :REM KAANNA
1130 GOSUB1050:REM LI$()->LI$
1140 RETURN
1150 ::
1160 A$=LEFT$(LI$,LEN(LI$)-1):B=0
1170 FORI=1TO10:LI$(I)="":NEXT
1180 B=B+1:FORI=1TOLEN(A$)
1182 IFMID$(A$,I,1)=" "THENGOTO1195
1190 NEXT:RETURN
1195 LI$(B)=LEFT$(A$,I-1):A$=MID$(A$,I+1,LEN(A$)-LEN(LI$(B))-1):GOTO1180
1200 ::
1210 REM LOYTYYKO AVAINSANA?
1220 ::
1230 A$="AVAIN EI":REM OLETUS
1240 REM JOKAISELLE AVAIMELLE
1250 FORI=2TOAM
1260 REM JOKAISELLE SYNONYYMILLE
1270 FORJ=1TOVAL(SN$(I,0))
1280 REM ONKO SYNO JONOSSA
1290 FORL=1TOLEN(L$)-LEN(SN$(I,J))+1
1300 IFSN$(I,J)=MID$(L$,L,LEN(SN$(I,J)))THENGOTO1325
1310 NEXTL,J,I
1320 RETURN
1325 AS$=SN$(I,J):A$="AVAIN ON":A=I:AS=A:GOTO1320
1330 ::
1340 REM TAULUKOI SYNONYYMIT
1350 ::
1360 A$=" "+AS$+"*":A=0
1370 A=A+1:FORL=1TOLEN(A$)
1375 IFMID$(A$,L,1)="*"THENGOTO1460
1380 NEXT
1390 REM === JA MUOTOILE NE ===
1400 FORL=1TOA
1402 IFRIGHT$(SN$(I,L),1)="-"THENGOTO1462
1410 SN$(I,L)=SN$(I,L)+" "
1420 SN$(I,L)=" "+SN$(I,L)
1430 NEXT
1440 SN$(I,1)=RIGHT$(SN$(I,1),LEN(SN$(I,1))-1)
1450 SN$(I,0)=STR$(A-1):RETURN
1460 SN$(I,A)=LEFT$(A$,L-1):A$=MID$(A$,L+1,LEN(A$)-LEN(SN$(I,A))-1):GOTO1370
1462 SN$(I,L)=LEFT$(SN$(I,L),LEN(SN$(I,L))-1):GOTO1420
1465 ::
1470 REM VALITSE LAUSE
1480 ::
1490 LOPUSSA$=""
1500 HO=VAL(RIGHT$(LM$(A,0),1)):HM=VAL(LEFT$(LM$(A,0),2))
1510 HO=HO+1:IFHO>HMTHENHO=1
1520 LM$(A,0)=STR$(HM)+"*"+STR$(HO)
1530 V$=LM$(A,HO):VA$=V$
1540 IFRIGHT$(V$,1)="="THENVA$=LEFT$(V$,LEN(V$)-1):LOPUSSA$="KOODI"
1550 AE%(A)=AE%(A)+1
1560 RETURN
1570 ::
1580 REM ARKISTOI VAST JA LIIT
1590 ::
1600 IFMV%(1)=ASORAS<2THENGOTO1650
1610 SY=SY+1
1620 FORI=5TO1STEP-1:MV%(I)=MV%(I-1):MV$(I)=MV$(I-1):NEXT
1630 MV%(1)=AS:AS=0
1640 MV$(1)=LL$:LL$=""
1650 RETURN
1670 ::
1680 REM MUISTELE JA VASTAA
1690 ::
1700 LV=LV+1:IFLV>2THENLV=1
1710 ON LV GOTO 1730,1770
1720 REM 1 == EDELLINEN AVAIN ==
1730 A=MV%(1):IFA=0THENGOTO1700
1740 IFMV$(1)=""THENLI$="":GOSUB1850:GOTO1810
1750 LI$=MV$(1)+" ":GOSUB1850:GOSUB1110:GOTO1810
1760 REM 2 == EDELLINEN LIITOS ==
1770 IFMV$(2)=""THENA=1:GOTO1810
1780 !$=" "+MV$(1):LI$=MV$(1)+" ":GOSUB1850:GOSUB1110
1790 GOSUB1230:REM ONKO AVAIN
1800 IFA$="AVAIN EI"THENGOTO1700
1810 L=0:RETURN
1815 ::
1840 REM *** NOSTO ***
1850 FORI=1TO4
1860 MV$(I)=MV$(I+1):MV%(I)=MV%(I+1):NEXT:MV$(5)="":MV%(5)=0:SY=SY-1:RETURN
1870 ::
1880 REM ONKO TOISTOA?
1890 ::
1900 TS$="":IFL$=EL$THENA=0:TS$="ON"
1910 EL$=L$:RETURN
1920 REM
1930 REM
1940 REM VASTAUS DATA
1950 REM
1960 DATA "JOS TOISTOA"
1970 DATA "ALA TOISTA ITSEASI!","SANOIT TUON JO KERRAN!","ALA VIITSI JANKUTTAA!"
1971 DATA "KEKSI JOTAIN MUUTA!","ETKO KEKSI MUUTA?",@
1980 DATA "EI AVAINTA"
1990 DATA "KERRO LISAA!MITA TUO MINKA KERROIT TUO SINULLE MIELEEN?"," JATKA!"
1991 DATA "NYT EN YMMARTANYT KAIKEA","SELITE TARKEMMIN"," TASMENNA"," OLE HYVA!"
2000 DATA "VOIT-*OSAAT-"
2010 DATA "ETKO USKO ETTA OSAAN=","EHKA SINA HALUAISIT=","HALUAT SIIS MINUN VOIVAN=",@
2020 DATA "VOIN-*OSAAN-"
2030 DATA "EHKET HALUE=","HALUAISITKO TODELLA=",@
2040 DATA "OLET-"
2050 DATA "MIKSI EN OLISI=","MIELLYTTAAKO SINUA ETTA OLEN="
2051 DATA "NAET MINUSSA VAIN HAAVEKUVASI-JOTAKIN LAPSUUDESTASI ?"
2052 DATA "TOIVOISITKO OLEVASI=","JOSKUS UNET TULEVAT TOSIKSI",@
2060 DATA "EN*MINA EN*MINA EN"
2070 DATA "ETKO TODELLAKAAN=","MIKSI ET=","HMM...KERROHAN JOTAIN HALUISTASI"
2071 DATA "HAIRITSEEKO SINUA ETTA=","VOITKO KUVITELLA MUUTA KUIN ETTET=",@
2080 DATA "MIKSI*MIKSEI"
2090 DATA "MITA AJATTELET KYSYESSASI TULLAISIA?","NIIN TODELLAKIN-MIKSI="
2091 DATA "SYVALLISIIN KYSYMYKSIIN ON SYVALLISET VASTAUKSET-JATKA"
2092 DATA "MIKSI YLEENSA MITAAN",@
2100 DATA "OLETKO"
2110 DATA "MIKSI SINUA KIINNOSTAA OLENKO VAI ENKO OLE="
2111 DATA "OLISIKO PAREMPI JOS EN OLISI=",@
2120 DATA "VIHA-"
2130 DATA "VIHANTUNNE JUONTAA USEIN JUURENSA LAPSUUSTRAUMOISTA"
2131 DATA "VIHAAMINEN ON HYODYTONTA ENERGIAN TUHLAUSTA","VIHAATKO ITSEASI?"
2132 DATA "VIHAATKO SUKULAISIA?","VIHAN VOI KAANTAA RAKKAUDEKSI",@
2140 DATA "HUUTA-*NALKUT-*RAYHA-*ROYHO-*ROHJO-"
2150 DATA "ON TARKEATA ETTA OSAA PURKAA TUNTEENSA"
2151 DATA "HMM..EHKA HIEMAN EPAHIENOA KAYTOSTA","SIIHEN ON VARMAAN OMAT SYYNSA",@
2160 DATA "RYYP-*JOI-*JUO-*VIIN-*ALKOH-"
2170 DATA "ALKOHOLIN KAYTTO HAITTAA YSTAVYYSSUHTEITA","PYSY SINA IRTI VIINASTA!!"
2171 DATA "MITEN TAMA LIITTYY MUIHIN ONGELMIISI","MIKSI LUULET ETTA IHMISET JUOVAT"
2172 DATA@
2173 DATA "KALJ-*OLUT-*PUB-*KAPAK-*RAVINTOL-"
2174 DATA "MIKA ON RYYPYTTELYN SYY?","KUN ON OLUTTA NIIN ON YSTAVIA - NIINKO?"
2175 DATA "KASVATTAAKO KALJA MAHAA?","NO AINAKIN ON SITTEN SEURAA - VAI ONKO?",@
2180 DATA "PETTA-*PETTO-*USKOTT-*USKOTON*VIERAISSA"
2190 DATA "HAIRAHDUKSIA SATTUU JOSKUS MEILLE KAIKILLE"
2191 DATA "TEIDAN OLISI SYYTA KESKUSTELLA ONGELMISTANNE KESKENANNE"
2192 DATA "MISTA LUULET USKOTTOMUUDEN JOHTUVAN?"
2193 DATA "MITA TUNNETTE TOISIANNE KOHTAAN?",@
2200 DATA "PELKA-*PELKO-*PELAN-*PELON-*PELKO-"
2210 DATA "OLETKO KAUANKIN PELANNYT=","PELKAATKO JUURI NYT="
2211 DATA "PELKO TUNTUU KAHLITSEVAN KAYTTAYTYMISTASI"
2212 DATA "OLETKO YRITTANYT VAPAUTUA PELOISTASI JOTENKIN?"
2213 DATA "TURHA HUOLESTUA-PELKAAMINEN ON IHAN TAVALLISTA",@
2220 DATA "USKO-"
2230 DATA "USKONASIOISTA EI KANNATA KIISTELLA","...USKOT="
2231 DATA "KERROHAN JOTAIN RIPPIKOULUSTASI"
2232 DATA "MITA TUNNET PASTORIASI KOHTAAN?",@
2240 DATA "MINUSTAA TUNTUU*TUNNEN"
2250 DATA "KERROHAN LISAA TUNTEMUKSISTASI","TUNTUUKO SINUSTA USEIN="
2251 DATA "NAUTITKO TUNTEMUKSISTASI?","TUNTEMINEN ON TARKEAA"
2252 DATA "AIHEUTTAAKO SE SINULLE SYYLLISYYDENTUNTEITA?",@
2260 DATA "SEKS-*SUKUPUOL-*RAKASTEL-*NAID-"
2270 DATA "EIKO TUO OLE HIUKAN PERVERSSIA?","TUNNETKO SEKSUAALISTA VETOA AITIISI?"
2271 DATA "TAIDAT OLLA KOKENUTKIN RAKASTAJA!"
2272 DATA "KERRO MINULLE FANTASIOISTASI"
2273 DATA "MITA VASTAKKAINEN SUKUPUOLI SINULLE SITTEN OIKEIN MERKITSEE?"
2274 DATA "MITEN LAPSUUDENKODISSASI SUHTAUDUTTIIN SUKUPUOLIASIOIHIN?",@
2280 DATA "HALU-*HIMO-*INTOHIMO-"
2290 DATA "MITA SINULLE MERKITSISI JOS SAISIT=","MIKSI HALUAT="
2291 DATA "OLETETAANPA ETTA SAISIT=","ENTAPA JOS ET KOSKAAN SAISI="
2292 DATA "MINAKIN HALUAN JOSKUS=",@
2300 DATA "UNEKS-*KUVIT-*UNI*KUVITELMA*UNTA"
2310 DATA "MITA UNESI TUO MIELEESI?","UNEKSITKO USEINKIN?"
2311 DATA "KEITA HAHMOJA UNISSASI ESIINTYY?HAIRITSEVATKO UNESI SINUA?"
2312 DATA "HIENOA-UNET PARANTAVAT!",@
2320 DATA "MIES-*MIEH-*AVIOM-*VAIMO-*PUOLISO-"
2330 DATA "MIKSI HAN=","MITA SINULLE MERKITSEE ETTA HAN="
2331 DATA "OLETKO PAHOILLASI ETTA HAN=","PETTAAKO HAN SINUA?"
2332 DATA "KUVAILISITKO HIUKAN TARKEMMIN SUHDETTANNE",@
2360 DATA "ISA-*ISO-*AITI-*AITI*VANHEM-"
2370 DATA "KERROHAN LISAA PERHEESTASI","MITEN SUHTAUDUIT VANHEMPIISI MURROSIASSA?"
2371 DATA "MITEN VANHEMPASI SUHTAUTUIVAT SINUUN?"
2372 DATA "ONGELMASI SAATTAA JOHTUA VANHEMPIESI VALISESTA SUHTEESTA-JATKA...",@
2380 DATA "SISKO-*VELI-*VELJE-*SYSTER-*BROIDI-"
2390 DATA "VOITKO TASMENTAA AJATUKSIASI SISARUKSISTASI?"
2391 DATA "OLETKO HALUNNUT SISARUKSIA?","KUINKA USEIN TAPAAT SISARUKSIASI?"
2392 DATA "MITEN SUKULAISESI SUHTAUTUVAT TAHAN?",@
2400 DATA "TYT-*TYTTOYSTAVA-*NAISE-"
2410 DATA "KERROHAN JOTAIN HAAVEISTASI","MITA TYTTOYSTAVA MERKITSEE SINULLE?"
2411 DATA "MITA KUVITTELET JUURI NYT?PITAISIKO TYTTOYSTAVASI MUISTUTTAA AITIASI?"
2412 DATA "KUVAILE SUHDETTASI VASTAKKAISEEN SUKUPUOLEEN",@
2420 DATA "POIK-*POJ-*POIKAYSTVA-*MIESYSTAVA-*MIEHE-"
2430 DATA "KUVAILISITKO TARKEMMIN TOIVEITASI","MITA POIKAYSTAVA MERKITSEE SINULLE?"
2431 DATA "MITA AJETTELET JUURI NYT?","PITAISIKO POIKAYSTAVASI MUISTUTTAA ISAASI?"
2432 DATA "KUVAILE SUHDETTASI VASTAKKAISEEN SUKUPUOLEEN",@
2440 DATA "YSTAV-"
2450 DATA "HALUAT EHKA KESKUSTELLA YSTAVISTASI?"
2451 DATA "OLET EHKA ETSIYTYNYT VAARANLAISTEN IHMISTEN PARIIN"
2452 DATA "KERROHAN MITA TUNNET YSTAVIASI KOHTAAN"
2453 DATA "TUO ON HYVIN TAVALLINEN HARHAKASITYS JA SE JOHTUU LIBIDON HARHAUMASTA"
2454 DATA "SINULLA ON LIIAN IDEAALINEN KUVA YSTAVYYDESTA",@
2460 DATA "SUKU-"
2470 DATA "MITA AJATTELET SUKULAISISTASI?KERROHAN JOTAIN LAPSUUDENMUISTOJASI"
2471 DATA "OLEN SAMAA MIELTA KANSSASI","TIEDAN-MINULLAKIN ON SUKULAISIA"
2472 DATA "PUHUTAAN VALILLA JOSTAKIN MUUSTA",@
2480 DATA "TIETOKONE-*KONE-"
2490 DATA "HUOLESTUTTAVATKO TIETOKONEET SINUA?"
2491 DATA "PELKAATKO TIETOKONEITTEN ORJUUTTAVAN IHMISIA?"
2492 DATA "PELOTTAVATKO KONEET SINUA","MIKSI MAINITSIT KONEET"
2493 DATA "MITA TEKEMISTA KONEILLA ON ONGELMIESI KANSSA",@
2500 DATA "EN VOI*EN OSAA"
2510 DATA "MISTA TIEDAT ETTET OSAA=","ONKO TASSA JOTAIN SYVEMPAA MERKITYSTA?"
2511 DATA "EHKA NYT OSAAT=","YRITTANYTTA EI LAITETA","MITA OIKEIN HALUAT ILMAISTA",@
2520 DATA "OLEN*MINA OLEN*OLIN*MINA OLIN"
2530 DATA "TAISIT TULLA TANNE KOSKA OLET=","KUINKA KAUAN OLET OLLUT="
2531 DATA "SYYLLISYYDEN TUNTEET JA PELOT SAATTAVAT KALVAA PAHASTI"
2532 DATA "ONKO SINUSTA NORMAALIA OLLA=","MITA VANHEMPASI SANOVAT SIIHEN ETTA OLET="
2533@
2540 DATA "SINA"
2550 DATA "KESKUSTELEMME SINUSTA EMMEKA MINUSTA","MINAKO="
2551 DATA "ETHAN VAIN PUHU MINUSTA",@
2560 DATA "MITA"
2570 DATA "MIKSI KYSYT?","KIINNOSTAAKO TUO KYSYMYS SINUA?"
2571 DATA "MIKA VASTAUS OLISI MIELLYTTAVIN?MITA LUULET?"
2572 DATA "KERROHAN JOTAKIN MITA TULEE NYT MIELEESI",@
2580 DATA "KUINKA*MISTA"
2590 DATA "AJATTELETKO USEINKIN TUOLLAISIA KYSYMYKSIA?"
2591 DATA "MITA SINA OIKEASTAAN HALUAT TIETAA?","OLETKO KYSYNYT KELTAAN MUULTA?"
2592 DATA "OLETKO KYSYNYT SELLAISTA AIKAISEMMIN?"
2593 DATA "MITA TULEE MIELEESI KYSYESSASI TUOLLAISTA?",@
2600 DATA "KUKA"
2610 DATA "NIMET EIVAT KIINNOSTA MINUA","EN VALITA MISTAAN NIMISTA-JATKA",@
2620 DATA "ANTEEKSI*OLEN PAHOILLANI"
2630 DATA "ALA TURHAAN PYYTELE ANTEEKSI","MIKSI OLET NIIN VAATIMATON?"
2631 DATA "MITA TUNTEMUKSIA SINULLA ON PYYTAESSASI ANTEEKSI?"
2632 DATA "ALA OLE NIIN PUOLUSTELEVA","MILTA SE TUNTUU?",@
2640 DATA "EHKA*EHKAPA*KENTIES*MELKO"
2650 DATA "ET VAIKUTA KOVINKAAN VARMALTA","MIKSI TUOLLAINEN SAVY?"
2651 DATA "ETKO VOISI OLLA VAHAN VARMEMPI","ETKO OLE VARMA?","ETKO TIEDA?",@
2660 DATA "EI"
2670 DATA "ASENNOIDUT HYVIN KIELTEISESTI","MIKSI MUUTEN EI?"
2671 DATA "MINA EN OLISI YHTA VARMA","VOISIT KOETTAA SUHTAUTUA HIUKAN MYONTEISEMMIN"
2672 DATA "TOTUUS ON TARUA IHMEELLISEMPAA!",@
2680 DATA "AINA*JOSKUS*USEIN"
2690 DATA "VOISITKO HIUKAN TASMENTAA?MILLOIN?MITA AJAT TAKAA?"
2691 DATA "IHAN AINAKO?KAIKKI EI AINA SUJU NIIN KUIN PITAISI",@
2700 DATA "MINUN MIELESTANI*MIELESTANI"
2710 DATA "OLETKO TOSISSASI SITA MIELTA?EPAILETKO ETTA="
2711 DATA "MIELIPITEET EROTTAVAT IHMISEN ELAIMISTA-JATKA"
2712 DATA "SINULLA ON JYRKKIA MIELIPITEITA!",@
2720 DATA "SAMANLAI-*SAMANKALTAI-"
2730 DATA "MILLA TAPAA?","MITA YHTEISTA HAVAITSET?"
2731 DATA "MITA SAMANLAISUUS TUO MIELEESI?","MITA MUITA YHTEYKSIA HUOMAAT?"
2732 DATA "TOKKOPA TUOLLAISOA YHTEIKSIA ONKAAN",@
2740 DATA "KYLLÃ„*KYLLO*JOO*TIETENKIN*TIETYSTI"
2750 DATA "VAIKUTAT AIKA ELAMANMYONTEISELTA","OLET AIVAN VARMA ETTA...="
2751 DATA "YMMARRAN - SIIS...=","TARKOITAT ETTA ... ="," TOISIN SANOEN...=",@
2752 DATA "RAHA-*VELA-*VELK-*PALK-*LASK-"
2753 DATA "MITA RAHA MERKITSEE SINULLE","MITA TEKISIT JOS SAISI MILJOONAN LOTOSSA"
2764 DATA "RAHA EI OLE KAIKKI KAIKESSA","OLETKO PYYTANYT PALKANKOROTUSTA",@
2780 DATA "RAKKA-*RAKA-"
2790 DATA "RAKKAUS ON IHMEELLINEN ASIA! KERRO LISAA RAKKAUDESTASI!"
2791 DATA "MILLAINEN OLI ENSIRAKKAUTESI?",
2792 DATA "MUISTA"," ETTA RAKKAUS MURTAA KAIKKI ESTEET!",@
2990 DATAL:REM LOPETUSKOODI
3000 FORI=1TOAM:PRINT"AVAIN";I:FORJ=1TOSM:PRINTSN$(I,J);"/";:NEXT:PRINT
3001 PRINT"==========================================="
3002 PRINT"LAUSEM*POINTTERI=";LM$(I,0):PRINT
3010 FORJ=1TOLM:PRINTJ;".."LM$(I,J):NEXT
3011 GETR$:IFR$=""THENGOTO3011
3012 NEXT